contract Token

constructor(uint _supply)

iff
    CALLVALUE == 0

creates
    mapping(address => uint) balanceOf :=  [CALLER => _supply]

transition balanceOf(address a) : uint256

iff

  CALLVALUE == 0

returns pre(balanceOf[a])


transition transfer(address to, uint256 value) : uint256

iff

  CALLVALUE == 0
  CALLER != to ==> inRange(uint256, balanceOf[to] + value)
  inRange(uint256,balanceOf[CALLER] - value)

case CALLER != to:

  storage

    balanceOf[CALLER]  := balanceOf[CALLER] - value
    balanceOf[to]    := balanceOf[to] + value

  returns 1

case CALLER == to:

  returns 1


contract TransferOneToken

constructor(address<Token> _tokenAddress)

iff

    CALLVALUE == 0
    _tokenAddress != 0

creates

    Token token := _tokenAddress

transition transfer() : uint256

iff
    CALLVALUE == 0    
    THIS != CALLER ==> inRange(uint256, token.balanceOf[CALLER] + 1)
    inRange(uint256,token.balanceOf[THIS] - 1)

case CALLER != THIS:

storage

    token.balanceOf[CALLER] := token.balanceOf[CALLER] + 1
    token.balanceOf[THIS]  := token.balanceOf[THIS] - 1

returns 1

case CALLER == THIS:

returns 1
