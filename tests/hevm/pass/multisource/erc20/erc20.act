contract Token

constructor
interface constructor(uint256 _totalSupply)

iff CALLVALUE == 0

creates

  uint256 totalSupply := _totalSupply
  mapping(address := uint) balanceOf :=  [CALLER := _totalSupply]
  mapping(address:=mapping(address:=uint)) allowance := []


transition transfer
interface transfer(uint256 value, address to)

iff

  CALLVALUE == 0
  inRange(uint256, balanceOf[CALLER] - value)
  CALLER =/= to := inRange(uint256, balanceOf[to] + value)

case CALLER =/= to:

  storage

     balanceOf[CALLER] := balanceOf[CALLER] - value
     balanceOf[to]     := balanceOf[to] + value

  returns 1

case CALLER == to:

  returns 1


transition transferFrom
interface transferFrom(address src, address dst, uint amount)

iff

  amount <= balanceOf[src]
  src    =/= dst := inRange(uint256, balanceOf[dst] + amount)
  CALLER =/= src := inRange(uint256, allowance[src][CALLER] - amount)
  CALLVALUE == 0

case src =/= dst and CALLER == src:

  storage

     balanceOf[src] := balanceOf[src] - amount
     balanceOf[dst] := balanceOf[dst] + amount

  returns 1

case src =/= dst and CALLER =/= src and allowance[src][CALLER] == 2^256 - 1:

  storage

     balanceOf[src] := balanceOf[src] - amount
     balanceOf[dst] := balanceOf[dst] + amount

  returns 1

case src =/= dst and CALLER =/= src and allowance[src][CALLER] < 2^256 - 1:

  storage

    allowance[src][CALLER] := allowance[src][CALLER] - amount
    balanceOf[src]         := balanceOf[src] - amount
    balanceOf[dst]         := balanceOf[dst] + amount

  returns 1

case src == dst and CALLER =/= src and allowance[src][CALLER] < 2^256 - 1:

  storage

    allowance[src][CALLER] := allowance[src][CALLER] - amount

  returns 1

case src == dst and (CALLER == src or allowance[src][CALLER] == 2^256 - 1):

  returns 1


transition approve
interface approve(address spender, uint amount)

iff

  inRange(uint256, amount)
  CALLVALUE == 0

case CALLER =/= spender:

  storage

    allowance[CALLER][spender] := amount

  returns 1

case CALLER == spender:

  returns 1


transition burn
interface burn(uint256 amount)

iff

  CALLVALUE == 0
  inRange(uint256, totalSupply - amount)
  inRange(uint256, balanceOf[CALLER] - amount)

storage

  totalSupply := totalSupply - amount
  balanceOf[CALLER] := balanceOf[CALLER] - amount

returns 1


transition burnFrom
interface burnFrom(address src, uint256 amount)

iff
  amount <= totalSupply
  amount <= balanceOf[src]
  CALLER =/= src := amount <= allowance[src][CALLER]
  CALLVALUE == 0

case CALLER =/= src and allowance[src][CALLER] == 2^256 - 1:

  storage

    totalSupply := totalSupply - amount
    balanceOf[src] := balanceOf[src] - amount

  returns 1

case CALLER =/= src and allowance[src][CALLER] < 2^256 - 1:

  storage

    totalSupply := totalSupply - amount
    allowance[src][CALLER] := allowance[src][CALLER] - amount
    balanceOf[src]         := balanceOf[src] - amount

  returns 1

case CALLER == src:

  storage

    totalSupply := totalSupply - amount
    balanceOf[src] := balanceOf[src] - amount

  returns 1

ensures

  pre(balanceOf[src]) - post(balanceOf[src]) == pre(totalSupply) - post(totalSupply)


transition mint
interface mint(address dst, uint256 amount)

iff in range uint256

  totalSupply + amount
  balanceOf[dst] + amount

iff

  CALLVALUE == 0

storage

  totalSupply := totalSupply + amount
  balanceOf[dst] := balanceOf[dst] + amount

returns 1


// View functions:

transition totalSupply
interface totalSupply()

iff 

  CALLVALUE == 0

returns pre(totalSupply)

transition balanceOf
interface balanceOf(address idx)

iff 

  CALLVALUE == 0

returns pre(balanceOf[idx])

transition allowance
interface allowance(address idx1, address idx2)

iff 

  CALLVALUE == 0

returns pre(allowance[idx1][idx2])
